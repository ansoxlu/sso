plugins {
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'war'
    id 'java'
}

if (!project.hasProperty('profile') || !profile) {
    ext.profile = 'dev'
}

allprojects {
    group 'com.example'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    ext {
        springCloudVersion = 'Greenwich.SR1'
        lombokVersion = '1.18.6'
        fastjsonVersion = '1.2.54'
        mysqlVersion = '8.0.13'
        adminVersion = '2.1.4'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    buildDir = "${rootProject.buildDir}/${project.name}"
    
    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies{
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    }
    
    processResources {
        filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [profile: profile]
    }
}
